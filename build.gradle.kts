import org.jetbrains.intellij.platform.gradle.TestFrameworkType

plugins {
    id("org.jetbrains.kotlin.jvm") version "2.3.20"
    id("org.jetbrains.intellij.platform")
    id("org.jetbrains.grammarkit") version "2023.3.0.3"
}

sourceSets {
    main {
        java {
            srcDirs("src/main/gen")
        }
    }
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdea("2026.1.3")
        bundledModule("intellij.platform.dap")
        testFramework(TestFrameworkType.Platform)
    }

    testImplementation("junit:junit:4.13.2")
}

intellijPlatform {
    pluginConfiguration {
        id = "io.github.unurgunite.crystal"
        name = "Fast Crystal Plugin"
        version = project.version.toString()
        vendor {
            name = "unurgunite"
            url = "https://github.com/unurgunite"
        }
        ideaVersion {
            sinceBuild = "261"
        }
    }

    signing {
        certificateChain.set(providers.environmentVariable("CERTIFICATE_CHAIN"))
        privateKey.set(providers.environmentVariable("PRIVATE_KEY"))
        password.set(providers.environmentVariable("PRIVATE_KEY_PASSWORD"))
    }

    publishing {
        token.set(providers.environmentVariable("PUBLISH_TOKEN"))
    }
}

tasks {
    runIde {
        systemProperty("idea.trust.all.projects", "true")
    }

    generateLexer {
        sourceFile.set(file("src/main/kotlin/io/github/unurgunite/crystal/lexer/Crystal.flex"))
        targetOutputDir.set(file("src/main/gen/io/github/unurgunite/crystal/lexer"))
    }

    generateParser {
        sourceFile.set(file("src/main/kotlin/io/github/unurgunite/crystal/parser/Crystal.bnf"))
        targetRootOutputDir.set(file("src/main/gen"))
        pathToParser.set("io/github/unurgunite/crystal/parser/CrystalParser.java")
        pathToPsiRoot.set("io/github/unurgunite/crystal/psi")
    }

    compileKotlin {
        dependsOn(generateLexer, generateParser)
    }

    withType<JavaCompile>().configureEach {
        options.isFork = false
    }
}

val defaultTest = tasks.named<Test>("test")
fun Test.stdlibTool(name: String) {
    group = "stdlib"
    val dt = defaultTest.get()
    testClassesDirs = dt.testClassesDirs
    classpath = dt.classpath
    jvmArgs(dt.allJvmArgs.filterNot { it.startsWith("-Xmx") } + "-Xmx4g" + "-Dgrammar.kit.gpub.max.level=6000")
    filter.includeTestsMatching("*StdlibGraphToolTest.$name")
}
tasks.register<Test>("stdlibParseErrors") { stdlibTool("testAggregateParseErrors") }
tasks.register<Test>("stdlibBuildGraph") { stdlibTool("testBuildGraph") }
tasks.register<Test>("stdlibStructure") { stdlibTool("testBuildStructureJson") }
tasks.register<Test>("stdlibCheckFile") {
    stdlibTool("testCheckSingleFile")
    systemProperty("graph.file", System.getProperty("graph.file") ?: "")
}

kotlin {
    jvmToolchain(21)
}
